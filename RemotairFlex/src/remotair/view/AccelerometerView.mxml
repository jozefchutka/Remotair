<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
         xmlns:s="library://ns.adobe.com/flex/spark" 
         xmlns:mx="library://ns.adobe.com/flex/mx"
         creationComplete="init()">
<fx:Script>
<![CDATA[
    import com.greensock.TweenMax;
    
    import sk.yoz.displacing.SimpleDisplacerTriangles;
    import sk.yoz.hardware.GeomagneticField;
    import sk.yoz.hardware.SensorManager;
    import sk.yoz.remotair.events.JoystickEvent;
    import sk.yoz.remotair.net.Receiver;
    
    [Embed(source="assets/china.jpg")]
    private static const IMAGE_CLASS:Class;
    
    [Embed(source="assets/chinaMap.jpg")]
    private static const MAP_CLASS:Class;
    
    private var displacer:SimpleDisplacerTriangles;
    private var _receiver:Receiver;
    private var geomagneticField:GeomagneticField;
    
    public function set receiver(value:Receiver):void
    {
        _receiver = value;
        
        receiver.accelerometerEvents.addEventListener(AccelerometerEvent.UPDATE, onAccelerometerUpdate);
        receiver.joystickEvents.addEventListener(JoystickEvent.CHANGE, onJoystickChange);
    }
    
    public function get receiver():Receiver
    {
        return _receiver;
    }
    
    private function init():void
    {
        displacer = new SimpleDisplacerTriangles(width, height,
            Bitmap(new IMAGE_CLASS).bitmapData, 
            Bitmap(new MAP_CLASS).bitmapData,
            90, 90, 100, 30, 30);
        container.addChild(displacer);
    }
    
    private function onAccelerometerUpdate(event:AccelerometerEvent):void
    {
        if(!displacer)
            return;
        
        container.x = width / 2;
        container.y = height / 2;
        displacer.x = -container.x;
        displacer.y = -container.y;
        
        
        TweenMax.to(displacer, .5, {
            rotX:-event.accelerationY * 90,
            rotY:event.accelerationX * 90});
        
        displacer.render();
        
        if(!geomagneticField)
            return;
        
        var R:Array = new Array(9);
        SensorManager.getRotationMatrix(
            event.accelerationX, event.accelerationY, event.accelerationZ,
            geomagneticField.x, geomagneticField.y, geomagneticField.z, R);
        var orientation:Array = SensorManager.getOrientation(R);
    }
    
    private function onJoystickChange(event:JoystickEvent):void
    {
        if(!displacer)
            return;
        
        var depth:Number = displacer.depth;
        var segmentsW:uint = displacer.segmentsW;
        var segmentsH:uint = displacer.segmentsH;
        
        if(event.joystickID == 1)
            depth = -event.y * 200 + 100;
        if(event.joystickID == 2)
        {
            segmentsW = Math.max(5, Math.abs(event.x * 50));
            segmentsH = Math.max(5, Math.abs(event.y * 50));
        }
        
        displacer.update(depth, segmentsW, segmentsH);
        displacer.render();
    }
]]>
</fx:Script>
<s:SpriteVisualElement id="container" />
</s:Group>